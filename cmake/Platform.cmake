IF(WIN32)
	IF(MSVC)
		IF((CMAKE_GENERATOR_PLATFORM STREQUAL "x64") OR (CMAKE_GENERATOR MATCHES "Win64"))
			SET(SAURON_ARCH_NAME "x64")
			SET(SAURON_VS_PLATFORM_NAME "x64")
		ELSEIF((CMAKE_GENERATOR_PLATFORM STREQUAL "ARM64") OR (CMAKE_GENERATOR MATCHES "ARM64"))
			SET(SAURON_ARCH_NAME "arm64")
			SET(SAURON_VS_PLATFORM_NAME "ARM64")
		ELSEIF((CMAKE_GENERATOR_PLATFORM STREQUAL "ARM") OR (CMAKE_GENERATOR MATCHES "ARM"))
			SET(SAURON_ARCH_NAME "arm")
			SET(SAURON_VS_PLATFORM_NAME "ARM")
		ELSE()
			MESSAGE(FATAL_ERROR "This CPU architecture is not supported")
		ENDIF()
	ENDIF()
	IF(WINDOWS_STORE)
		SET(SAURON_PLATFORM_NAME "win_store")
		SET(SAURON_PLATFORM_WINDOWS_STORE TRUE)
	ELSE()
		SET(SAURON_PLATFORM_NAME "win")
		SET(SAURON_PLATFORM_WINDOWS_DESKTOP TRUE)
	ENDIF()
	SET(SAURON_PLATFORM_WINDOWS TRUE)
ELSEIF(ANDROID)
	SET(SAURON_PLATFORM_NAME "android")
	SET(SAURON_PLATFORM_ANDROID TRUE)
ELSEIF(${CMAKE_SYSTEM_NAME} MATCHES "Linux")
	SET(SAURON_PLATFORM_NAME "linux")
	SET(SAURON_PLATFORM_LINUX TRUE)
ELSEIF(IOS)
	SET(SAURON_PLATFORM_NAME "ios")
	SET(SAURON_PLATFORM_IOS TRUE)
ELSEIF(${CMAKE_SYSTEM_NAME} MATCHES "Darwin")
	IF(IOS)
		SET(SAURON_PLATFORM_NAME "ios")
		SET(SAURON_PLATFORM_IOS TRUE)
	ELSE()
		SET(SAURON_PLATFORM_NAME "darwin")
		SET(SAURON_PLATFORM_DARWIN TRUE)
	ENDIF()
ENDIF()

IF(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Windows")
	SET(SAURON_HOST_PLATFORM_NAME "win")
	SET(SAURON_HOST_PLATFORM_WINDOWS TRUE)
ELSEIF(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Linux")
	SET(SAURON_HOST_PLATFORM_NAME "linux")
	SET(SAURON_HOST_PLATFORM_LINUX TRUE)
ELSEIF(${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Darwin")
	SET(SAURON_HOST_PLATFORM_NAME "darwin")
	SET(SAURON_HOST_PLATFORM_DARWIN TRUE)
ENDIF()

IF(NOT SAURON_ARCH_NAME)
	IF((CMAKE_SYSTEM_PROCESSOR MATCHES "AMD64") OR (CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64"))
		SET(SAURON_ARCH_NAME "x64")
	ELSE()
		SET(SAURON_ARCH_NAME "x86")
	ENDIF()
ENDIF()

IF((CMAKE_HOST_SYSTEM_PROCESSOR MATCHES "AMD64") OR (CMAKE_HOST_SYSTEM_PROCESSOR MATCHES "x86_64"))
	SET(SAURON_HOST_ARCH_NAME "x64")
ELSE()
	SET(SAURON_HOST_ARCH_NAME "x86")
ENDIF()

SET(SAURON_PLATFORM_NAME ${SAURON_PLATFORM_NAME}_${SAURON_ARCH_NAME})
SET(SAURON_HOST_PLATFORM_NAME ${SAURON_HOST_PLATFORM_NAME}_${SAURON_HOST_ARCH_NAME})

IF(SAURON_PLATFORM_ANDROID OR SAURON_PLATFORM_IOS)
	SET(SAURON_PREFERRED_LIB_TYPE "STATIC")
ELSE()
	SET(SAURON_PREFERRED_LIB_TYPE "SHARED")
ENDIF()

IF (SAURON_PLATFORM_WINDOWS_DESKTOP OR SAURON_PLATFORM_LINUX OR SAURON_PLATFORM_DARWIN)
	SET(SAURON_IS_DEV_PLATFORM TRUE)
ELSE()
	SET(SAURON_IS_DEV_PLATFORM FALSE)
ENDIF()
